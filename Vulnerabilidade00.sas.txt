LIBNAME OUT 'C:\Projetos\Projeto a ser autorizado\Programas\Vulnerabilidade\2000';
LIBNAME IN 'C:\Projetos\Projeto a ser autorizado\Programas\Base dom e pes 00';
LIBNAME IN2 'C:\Projetos\Projeto a ser autorizado\Programas\Renda imputada\2000';

 OPTIONS PS=10000 NODATE LS=250 OBS=MAX;

DATA IBGE(DROP=V0102 V0103 V0104 V0105 V0106);
SET IN.pesdom00;


 ********************;
 * VARIAVEIS DE     *;
 * SETOR CENSITARIO *;
 ********************;

UF=PUT(V0102,$2.);
DIST=PUT(V0104,$2.);
MUNIC=PUT(V0103,$4.);
SUBDIST=PUT(V0105,$2.);
SETOR=PUT(V0106,$4.);
QUEST=PUT(V0101,$4.);

 SETORC=UF!!MUNIC!!DIST!!SUBDIST!!SETOR;

 IDDOM=UF!!MUNIC!!DIST!!SUBDIST!!SETOR!!QUEST;



DATA RDPCIMPUT(KEEP=IDDOM RDPC_IMPUT);
SET IN2.rdpcimput;



DATA PES;
SET IBGE;


****************;
* PESOP - PESO *;
****************;
PESOPES=P001;


*** CONTADOR ***;
CONT=1;


*** EXCLUINDO PENSIONISTA, EMPREGADO DOMESTICO E PARENTES DO EMPREGADO ***;

IF V0402<=8;


*** EXCLUINDO OS DOMICILIOS COLETIVOS ***;
IF V0201=1;

********************* SEM FUNDAMENTAL COMPLETO *********************;

 SELECT;
  WHEN (V0429=4)             FUNDIN=1;
  WHEN (1<=V0430<=7)         FUNDIN=1;
  WHEN (V0432=9)             FUNDIN=1;
  WHEN (1<=V0432<=2)         FUNDIN=1;
  WHEN (V0432=3 AND V0434=2) FUNDIN=1;
  WHEN (V0432=5 AND V0434=2) FUNDIN=1;
  OTHERWISE                  FUNDIN=0;
 END;


*** MULHERES COM FILHOS ***;


SELECT;
 WHEN ((V4690>0) AND (10<=V4752<=14) AND (V0401=2)) M10A14CF=1;
 OTHERWISE                                          M10A14CF=0;
END;

SELECT;
 WHEN ((V4690>0) AND (15<=V4752<=17) AND (V0401=2)) M15A17CF=1;
 OTHERWISE                                          M15A17CF=0;
END;

SELECT;
 WHEN ((V0401=2) AND (10<=V4752<=14)) POPMU10A14=1;
 OTHERWISE                            POPMU10A14=0;
END;

SELECT;
 WHEN ((V0401=2) AND (15<=V4752<=17)) POPMU15A17=1;
 OTHERWISE                            POPMU15A17=0;
END;



*** MULHER CHEFE ***;


IF (V0402=1 AND V0401=2) THEN MULCHEFE=1;
ELSE                          MULCHEFE=0;


IF (V0402=1 AND V0401=2 AND FUNDIN=1) THEN MULCHEFEFUNDIN=1;
ELSE                                       MULCHEFEFUNDIN=0;



SELECT;
 WHEN (V4752>=10 AND V0401=2) POPMU10M=1;
 OTHERWISE                    POPMU10M=0;
END;


IF (0<=V4752<=14 AND V0402=3) THEN F014=1;
ELSE                               F014=0;


IF (V4752>14) THEN ADULTO=1;
ELSE               ADULTO=0;



*** PESSOAS COM MENOS DE 15 ANOS QUE TRABALHAM ***;


IF (V0439=1 OR V0440=1 OR V0441=1 OR V0442=1 OR V0443=1) THEN OCUP=1;
ELSE                                                          OCUP=0;


SELECT;
 WHEN ((0<=V4752<=14) AND (OCUP=1)) OCUPM15=1;
 OTHERWISE                          OCUPM15=0;
END;



IF (10<=V4752<=14) THEN POP10A14=1;
ELSE                    POP10A14=0;


IF (15<=V4752<=24) THEN POP15A24=1;
ELSE                    POP15A24=0;


IF (V4752>=18) THEN POP18M=1;
ELSE                POP18M=0;


IF (V4752<15) THEN POPM15=1;
ELSE               POPM15=0;



*** NAO FREQUENTA ***;

SELECT;
  WHEN (1<=V0430<=10 OR V0430=12 OR V0430=13)  FREQ=1;
  OTHERWISE                                    FREQ=0;
END;


IF FREQ=. THEN FREQ=0;


****************** OCUPADOS ******************;


*******;
* DES *;
*******;

SELECT;
 WHEN (V0439=2 AND V0440=2 AND V0441=2 AND V0442=2 AND V0443=2 AND V0455=1) DES=1;
 OTHERWISE                                                                  DES=0;
END;


*******;
* PEA *;
*******;

SELECT;
 WHEN (OCUP=1 OR DES=1) PEA=1;
 OTHERWISE              PEA=0;
END;


SELECT;
 WHEN (V0447=1 OR V0447=3 OR V0448=1 OR ((V0447=6 OR V0447=5) AND V0450=1)) FORMAL=1;
 OTHERWISE                                                                  FORMAL=0;
END;


IF PEA=0 THEN NPEA=1;
ELSE          NPEA=0;


IF FORMAL=0 THEN INFORMAL=1;
ELSE             INFORMAL=0;




IF (V4752>=65) THEN IDOSO=1;
ELSE                IDOSO=0;


IF V4614=999999 THEN V4614=.;
IF V4614=. THEN V4614=0;


PROC SORT DATA=PES;
BY IDDOM;


PROC MEANS DATA=PES SUM N NOPRINT;
VAR CONT V4614 ADULTO;
OUTPUT OUT=SOMA1(KEEP=IDDOM SCONT POPDOM RDOM ADULTODOM)
       SUM=SCONT RDOM ADULTODOM
       N=POPDOM;
BY IDDOM;



PROC MEANS DATA=PES SUM NOPRINT;
VAR MULCHEFEFUNDIN MULCHEFE F014 FUNDIN;
OUTPUT OUT=SOMA2(KEEP=IDDOM P_MULCHEFEFUNDIN P_MULCHEFE P_F014 SFUNDIN)
       SUM=P_MULCHEFEFUNDIN P_MULCHEFE P_F014 SFUNDIN;
BY IDDOM;


DATA VUL;
 MERGE PES SOMA1 SOMA2 RDPCIMPUT;
 BY IDDOM;



RDPC1=(RDOM/POPDOM)*1.95209;
RDPC=RDPC_IMPUT*1.95209;



PROC MEANS DATA=VUL SUM NOPRINT;
VAR RDPC;
OUTPUT OUT=REN(KEEP=IDDOM RENDADOM)
       SUM=RENDADOM;
BY IDDOM;


DATA VUL;
MERGE VUL REN;
BY IDDOM;



****** IMPUTANDO A RENDA PESSOAL ******;

SELECT;
  WHEN ((RDPC1=0 AND RDPC>0) AND V4752<=14) RENDAP=0;
  WHEN ((RDPC1=0 AND RDPC>0) AND V4752>14)  RENDAP=RENDADOM/ADULTODOM;
  WHEN ((RDPC1=0 AND RDPC=0) OR RDPC1>0)    RENDAP=V4614*1.95209;
  OTHERWISE                                 RENDAP=.;
END;



IF (P_MULCHEFEFUNDIN=1 AND P_F014>0) THEN MULCHEFEFUNDINFDOM=1;
ELSE                                      MULCHEFEFUNDINFDOM=0;


IF (SFUNDIN=SCONT)  THEN FUNDIN_TODOS=1;
ELSE                     FUNDIN_TODOS=0;


IF (V4752>=18 AND FUNDIN=1 AND INFORMAL=1) THEN FUNDIN18MINF=1;
ELSE                                            FUNDIN18MINF=0;


IF (FUNDIN_TODOS=1 AND RDPC<=255) THEN FUNDIN_TODOS_MMEIO=1;
ELSE                                   FUNDIN_TODOS_MMEIO=0;



*********** CRIANÇAS ***********;

IF (FUNDIN_TODOS=1 AND V4752<15) THEN CRIFUNDIN_TODOS=1;
ELSE                                  CRIFUNDIN_TODOS=0;


IF (FREQ=0 AND (DES=1 OR NPEA=1) AND RDPC<=255 AND POP15A24=1) THEN NESTUDA_NTRAB_MMEIO=1;
ELSE                                                                NESTUDA_NTRAB_MMEIO=0;


IF ((RENDAP>=RENDADOM*0.5) AND IDOSO=1) THEN MAX_RDPC_IDOSO=1;
ELSE                                         MAX_RDPC_IDOSO=0;



PROC MEANS DATA=VUL SUM NOPRINT;
VAR MAX_RDPC_IDOSO;
OUTPUT OUT=SOMA3(KEEP=IDDOM DOM_IDOSO)
       SUM=DOM_IDOSO;
BY IDDOM;



DATA JUNTO;
MERGE VUL SOMA3;
 BY IDDOM;


*** CONTADOR ***;
CONTADOR=1;



IF (RDPC<=255 AND DOM_IDOSO>=1) THEN RMAXIDOSO=1;
ELSE                                 RMAXIDOSO=0;


PROC SORT DATA=JUNTO;
BY SETORC;


PROC MEANS DATA=JUNTO SUM NOPRINT;
VAR OCUPM15 FUNDIN_TODOS FUNDIN18MINF FUNDIN_TODOS_MMEIO CRIFUNDIN_TODOS NESTUDA_NTRAB_MMEIO RMAXIDOSO
    POP18M POP10A14 POP15A24 POPM15 OCUP CONTADOR M10A14CF M15A17CF MULCHEFEFUNDINFDOM
    POPMU10A14 POPMU15A17 POPMU10M;
OUTPUT OUT=SOMA4(KEEP=SETORC OCUPM15 FUNDIN_TODOS FUNDIN18MINF FUNDIN_TODOS_MMEIO CRIFUNDIN_TODOS NESTUDA_NTRAB_MMEIO RMAXIDOSO
                      POP18M POP10A14 POP15A24 POPM15 OCUP POP M10A14CF M15A17CF MULCHEFEFUNDINFDOM
                      POPMU10A14 POPMU15A17 POPMU10M)
       SUM=OCUPM15 FUNDIN_TODOS FUNDIN18MINF FUNDIN_TODOS_MMEIO CRIFUNDIN_TODOS NESTUDA_NTRAB_MMEIO RMAXIDOSO
           POP18M POP10A14 POP15A24 POPM15 OCUP POP M10A14CF M15A17CF MULCHEFEFUNDINFDOM
           POPMU10A14 POPMU15A17 POPMU10M;
WEIGHT PESOPES;
BY SETORC;



DATA OUT.VUL00;
SET SOMA4;


RUN;
